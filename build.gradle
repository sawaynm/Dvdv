buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven {
        url "https://maven.google.com"
    }
    //Snowfall
    maven { url "https://jitpack.io" }
    google()
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'org.thoughtcrime.ssl.pinning:AndroidPinning:1.0.0'
    implementation 'com.google.android.gms:play-services-location:19.0.1'
    implementation 'joda-time:joda-time:2.10.13'
    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    //Snowfall
    implementation 'com.github.jetradarmobile:android-snowfall:1.2.1'
}

android {

    compileSdkVersion 32

    buildFeatures {
        dataBinding true
        // for view binding:
        // viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 32
        //noinspection HighAppVersionCode
        versionCode 2022030200
        versionName "2022.3"
        multiDexEnabled true
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

    }

    signingConfigs {

        debug {
            storeFile file("${System.properties['user.home']}" + "/.android/debug.keystore")
        }

        /* note:  To build a signed version of NetHunter:
        1. Put your keystore in ~/.android/release-keystore and set up a key "NetHunter"
        2. set KEYSTORE_PASSWORD and KEY_PASSWORD environment variables before running gradle.  this
           can be done easily by adding them to .bashrc
        (or change the below values, hard-coding or whatever...)
        3. Don't forget to create a proguard.cfg file.  You can find a sample one in the sdk or
           point to it directly.
        release {
            storePassword System.getenv("KEYSTORE_PASSWORD")
            storeFile file("${System.properties['user.home']}" +  "/.android/release.keystore")
            keyAlias "NetHunter"
            keyPassword System.getenv("KEY_PASSWORD")

        }
       */ 
    }

    buildTypes {

        debug {
            minifyEnabled false
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
            buildConfigField "String", "BUILD_NAME", "\"" + System.getenv("USER") + "\""

            return true
        }
        release {
            //signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            debuggable false
            jniDebuggable false
            zipAlignEnabled true
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
            buildConfigField "String", "BUILD_NAME", "\"" + System.getenv("USER") + "\""

            return true
        }
    }
}
